
program = decl*:d operation*:o {VnemoProg(:d, :o)};

// Types
decl = ws "var" ws letters$l ws ":" ws type:t ws ";" ws {VnemoVarDecl($l, :t)};
type = int | array;
int = ws "int" ws {VnemoIntType()};
array = ws "[" type:t "]" ws {VnemoArrayType(:t)};
mark = ws ('0'-'9')+$d ws ","* ws {VnemoLabel(s2i($d))};

// Operations
operation = cond | print | assign ;
condop = ws expr:le ws logOp$o expr:re ws {VnemoCond($o, :le, :re)};
cond = ws digits$l ws ":" ws "if" ws "(" condop:c ")" ws "then" ws "goto" ws "{" mark*:le "}" 
    ws "else" ws "goto" ws "{" mark*:lr "}" ws ";" ws {VnemoIfElse(s2i($l), :c, :le, :lr)};
print = ws digits$l ws ":" ws "print(" expr:e ")" ws "goto" ws "{" mark*:ls "}" ws ";" ws {VnemoPrint(s2i($l), :e, :ls)};
assign = ws digits$l ws ":" ws var:v ws "=" expr:e "goto" ws "{" mark*:ls "}" ws ";" ws {VnemoAssign(s2i($l), :v, :e, :ls)};

// Expressions
expr = const | var | binop | app | upd;
const = ws digits$d ws {VnemoConst(s2i($d))};
var = ws letters$l ws {VnemoVar($l)};
binop = ws "(" expr:l arOp$s expr:r ")" ws {VnemoBinOp($s, :l, :r)};
app = ws "(" ws "app" ws "(" expr:l "," expr:r ")" ws ")" ws {VnemoApp(:l, :r)};
upd = ws "(" ws "upd" ws "(" expr:l "," expr:m "," expr:r ")" ws ")" ws {VnemoUpd(:l, :m, :r)};

// Base
digits = ('0'-'9')+;
letters = ('a'-'z')+;
logOp = ("==" | "<=" | ">=" | "<" | ">" | "!=");
arOp = ("+" | "*" | "/" | "-" | "%");

// Whitespace
ws = (' ' | '\t' | '\n' | '\r')*;